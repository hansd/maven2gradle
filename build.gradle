import java.text.SimpleDateFormat

apply plugin: 'application'
apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'idea'

group = 'org.gradle.tools'
archivesBaseName = 'maven2gradle'
version = '1.0-alpha-1'

applicationName = 'maven2gradle'
mainClassName = 'org.gradle.tools.Maven2Gradle'

sourceCompatibility = 1.6
targetCompatibility = 1.6

repositories {
  mavenCentral()
}

dependencies {
  groovy localGroovy()
  compile group: 'org.apache.ant', name: 'ant', version: '1.8.2'
}

task copyJarToGradleHome(type: Copy, dependsOn: build) {
    description = 'Copy output JAR to $GRADLE_HOME/lib'
    from jar
    into "$gradleHome/lib"
}

task copyScriptsToGradleHome(type: Copy) {
    description = 'Copy shell scripts to $GRADLE_HOME/bin/'
    from startScripts.outputs.files
    into "$gradleHome/bin"
}

task copy2GradleHome(dependsOn: [copyJarToGradleHome, copyScriptsToGradleHome]) {
    description = 'Copies the maven2gradle jar as well as the maven2gradle start scripts to the local GRADLE_HOME'
}

def getGradleHome() {
    System.getenv()['GRADLE_HOME']
}

task wrapper(type: Wrapper)